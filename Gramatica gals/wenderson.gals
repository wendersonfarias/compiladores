#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = false
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
espacobranco : [\s\t\n]
comentarios    : [\0-\41\43-\255]
caractere  : [a-zA-Z]
inteiro    : [0-9]

#Tokens
                       	: {espacobranco}*
                        	: / / [^\n]*
identificador             	: {caractere}({caractere} | {inteiro} )*
numero         		: ({inteiro})+


inicioprograma	= identificador : "iniciop"


fimprograma	= identificador : "fimp"
fimse     	= identificador : "fimse"
fimlaco  	= identificador : "fimlaco"

se       	= identificador : "se"
senao     	= identificador : "senao"


laco     	= identificador : "laco"
leia    		= identificador : "leia"

escreva    	= identificador : "escreva"

var      	= identificador : "var"
faca  		= identificador : "faca"	
entao		= identificador : "entao"	

"("
")"
";"
"="
"=="
"+"
"-"
"*"
"/"
"<"
"<="
">="
">"


#NonTerminals
<programa>
<declaracao_recursiva>
<declaracao>
<expressao>
<senao>
<termo>
<proximo_termo>
<mais_menos>
<fator>
<proximo_fator>
<multi_div>
<expressao_logica>
<operador_logico>
#Grammar
<programa>::= inicioprograma <declaracao_recursiva> fimprograma 
|	î
;
<declaracao_recursiva>::= <declaracao> ";" <declaracao_recursiva> |î
;

<declaracao>::= identificador  "=" <expressao> 
;

<declaracao>::= leia "(" identificador ")"
;
<declaracao>::= escreva  <expressao>   
;
<declaracao>::= var identificador
;
//Laco enquanto
<declaracao>::= laco <expressao_logica>  faca <declaracao_recursiva> fimlaco

;

// condicional se(){} senao ou vazio
<declaracao>::= se <expressao_logica> entao <declaracao_recursiva> <senao> fimse
;
<senao>::= senao <declaracao_recursiva>   |  î
;
<expressao>::= <termo> <proximo_termo>
;
<proximo_termo>::= <mais_menos> <termo> <proximo_termo> | î
;
<termo>::= <fator> <proximo_fator>
;
<proximo_fator>::=  <multi_div> <fator> <proximo_fator> | î
;
<fator>::= "(" <expressao> ")"
;
<fator>::= identificador
;
<fator>::= numero
;
<mais_menos>::= "+" | "-" 
;
<multi_div>::= "*" | "/"
;
<expressao_logica>::= "(" <expressao> <operador_logico> <expressao> ")" 
;
<operador_logico>::= "<" | "<=" | ">=" | ">" | "=="
;
